(defun entity:getdxf (ent i / getdxf result)
  "获取图元的组码值\n参数:\nent:图元名或vla对象名\ni:组码或组码表"
  "组码值或列表"
  "(entity:getdxf (car (entsel))
    10)"
  (defun getdxf (ent i)
    (mapcar (quote cdr)
      (vl-remove-if-not (quote (lambda (x)
            (= (car x)
              i)))
        ent)))
  (cond ((p:vlap ent)
      (setq ent (entget (vlax-vla-object->ename ent)
          (quote ("*")))))
    ((p:enamep ent)
      (setq ent (entget ent (quote ("*"))))))
  (cond ((atom i)
      (setq result (getdxf ent i)))
    ((listp i)
      (setq result (apply (quote append)
          (mapcar (quote (lambda (x)
                (getdxf ent x)))
            i)))))
  (if (= 1 (length result))
    (car result)
    result))
