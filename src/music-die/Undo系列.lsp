(defun UndoBegin()
	(vla-StartUndoMark (*DOC*))
)
(defun UndoEnd()
	(vla-EndUndoMark (*DOC*))
)

; 可撤回一步操作，并且不会使视口移动
(defun UNDO1(/ ctr vsize)
	(setq CTR (U2W (getvar "VIEWCTR")))
	(setq Vsize (getvar "VIEWSIZE"))
	(vl-cmdf "UNDO" 1)
	;(COMMAND-SWITCH "ZOOM" "C" CTR Vsize)
	(vla-ZoomCenter (*ACAD*) (vlax-make-variant(LIST->VAR (LIST ctr))) Vsize)
)

(defun U2W(POINT)
	(trans POINT 1 0)
)
(defun LIST->VAR(LISTS / pntlst2)		
	(vlax-safearray-fill
		(vlax-make-safearray
			vlax-vbDouble
			(cons 0 (1- (* 3 (length LISTS))))
		)
		(foreach e LISTS
			(setq PntLst2 (append PntLst2 e))
		)
	)
)

(mapcar '(lambda(SYM Arg)
			 (eval (list 'defun SYM 'nil Arg))
		 )
	(list '*ACAD* '*DOC* '*DOCS* '*MS* '*PS* '*BLKS* '*LAYS* '*LTS* '*TSS* '*DIMS* '*VPS* '*DICS* '*Layouts* '*AS* '*shell* '*DBX* '*WMI*)
	(LIST
		'(vlax-get-acad-object) 
		'(vla-get-ActiveDocument (*ACAD*))
		'(vla-get-Documents (*ACAD*))
		'(vla-get-modelSpace (*DOC*))
		'(vla-get-paperSpace (*DOC*))
		'(vla-get-Blocks (*DOC*))
		'(vla-get-Layers (*DOC*))
		'(vla-get-Linetypes (*DOC*))
		'(vla-get-TextStyles (*DOC*))
		'(vla-get-DimStyles (*DOC*))
		'(vla-get-Viewports (*DOC*))
		'(vla-get-Dictionaries (*DOC*))
		'(vla-get-Layouts (*DOC*))
		'(if (= 0 (getvar "TILEMODE"))(*PS*)(*MS*))
		'(vlax-create-object "Shell.Application")
		'(vlax-create-object (strcat "ObjectDBX.AxDbDocument." (substr (getvar "ACADVER") 1 2)))
		'(vlax-create-object "WbemScripting.SWbemLocator")
	)
)